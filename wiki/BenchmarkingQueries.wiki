#summary One-sentence summary of this page.

= Query 1 =
{{{
# query 1
# Select feature short name, unique name, annotation ID, and official
# full name, given any label and where feature is D. melanogaster
# gene.

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX chado: <http://purl.org/net/chado/schema/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>    
PREFIX so: <http://purl.org/obo/owl/SO#>
PREFIX syntype: <http://purl.org/net/flybase/synonym-types/>

SELECT DISTINCT ?uniquename ?name ?accession ?fullname WHERE {

  ?feature skos:altLabel "aly" ; 
    a so:SO_0000704 ;
    chado:organism <http://openflydata.org/id/flybase/organism/Drosophila_melanogaster> ;
    chado:uniquename ?uniquename ;
    chado:name ?name ; 
    chado:feature_dbxref [ 
      chado:accession ?accession ; 
      chado:db <http://openflydata.org/id/flybase/db/FlyBase_Annotation_IDs> 
    ] .

  OPTIONAL {
    ?fs 
      chado:feature ?feature ; 
      chado:is_current "true"^^xsd:boolean ;
      chado:synonym [ 
        a syntype:FullName ;
        chado:name ?fullname ; 
      ] ;
      a chado:Feature_Synonym .
  }

}
}}}


{{{
SELECT distinct f.uniquename, f.name, d.accession, s.name FROM feature f, organism o, feature_dbxref fd, dbxref d, db db, feature_synonym fs, synonym s
WHERE f.organism_id = o.organism_id and o.genus = 'Drosophila' and o.species = 'melanogaster' and f.name = 'aly' and 
f.feature_id = fd.feature_id and fd.dbxref_id = d.dbxref_id and d.db_id = db.db_id and db.name='FlyBase Annotation IDs'
and f.feature_id = fs.feature_id and fs.is_current and fs.synonym_id = s.synonym_id and s.type_id='60037'
}}}

||1547||
||1484||
||1469||
||1484||
||1484||
||1500||
||1484||
||1485||
||1516||
||1485||

= Query 2 =
{{{
# query 2
# select publications for a given D. melanogaster gene

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX chado: <http://purl.org/net/chado/schema/>
PREFIX so: <http://purl.org/obo/owl/SO#>

SELECT * WHERE {
  
  ?feature 
    chado:uniquename "FBgn0010774"^^xsd:string ;
    a so:SO_0000704 ;
    chado:organism <http://openflydata.org/id/flybase/organism/Drosophila_melanogaster> ;
    chado:feature_pub ?pub .

  ?pub a chado:Pub ;
    chado:uniquename ?uniquename .
  
  OPTIONAL { ?pub chado:title ?title }
  OPTIONAL { ?pub chado:pyear ?pyear }
  OPTIONAL { ?pub chado:pages ?pages }
  OPTIONAL { ?pub chado:volume ?volume }
  OPTIONAL { ?pub chado:issue ?issue }
  OPTIONAL { ?pub chado:miniref ?miniref }

}
}}}

{{{
select distinct feature.uniquename, feature_pub.feature_id, pub.pub_id, pub.title, pub.pyear, pub.pages, pub.volume, pub.issue, pub.miniref
from feature_pub, pub, feature
where feature_pub.pub_id = pub.pub_id and feature.feature_id = feature_pub.feature_id and feature.uniquename = 'FBgn0015799'
}}}


1594,1344,1468,1609,1469,1344,1344,1360,1343,1344
  
= Query 3 =
{{{
# query 3
# select location data for a given D. melanogaster gene

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX chado: <http://purl.org/net/chado/schema/>
PREFIX fbp: <http://purl.org/net/flybase/property-types/>   
PREFIX so: <http://purl.org/obo/owl/SO#>
 
SELECT * WHERE {
  ?feature 
    chado:uniquename "FBgn0015269"^^xsd:string ;
    a so:SO_0000704 ;
    chado:organism <http://openflydata.org/id/flybase/organism/Drosophila_melanogaster> .
    
  ?featureloc 
    chado:feature ?feature ;
    a chado:Featureloc ;
    chado:fmin ?fmin ;
    chado:fmax ?fmax ;
    chado:locgroup ?locgroup ;
    chado:strand ?strand ;
    chado:srcfeature ?srcfeature .
    
  ?srcfeature
    a chado:Feature ;
    chado:name ?srcfeature_name ;
    chado:uniquename ?srcfeature_uniquename ;
    chado:seqlen ?srcfeature_seqlen .
}
}}}

{{{
select distinct f.feature_id, fl.fmin, fl.fmax, fl.locgroup, fl.strand, fl.scrfeature
from feature f, featureloc fl, organism o
where f.feature_id = fl.feature_id and f.organism_id = o.organism_id and o.genus = 'Drosophila' and o.species = 'melanogaster' and f.uniquename = 'FBgn0015269'
}}}